BACKUP "weidu_external/backup/ZS_ShadowdancerOverhaul"
AUTHOR "RoyalProtector at Gibberlings's forum https://www.gibberlings3.net/profile/12720-royalprotector/"
VERSION "v1.0"
README ~ZS_ShadowdancerOverhaul/README.md~
/////////////////////////////////////////////////////////////////////////////////////////////////////
AUTO_EVAL_STRINGS
ALWAYS
    INCLUDE "ZS_ShadowdancerOverhaul/libs/zs_functions.tph"
END
/////////////////////////////////////////////////////////////////////////////////////////////////////
LANGUAGE
"English"
"english"
"ZS_ShadowdancerOverhaul/lang/english/setup.tra"
/////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1000 DESIGNATED 100
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
/////////////////////////////////////////////////////////////////////////////////////////////////////
// COPYING ALL THE RELEVANT FILES

COPY "%MOD_FOLDER%/spl" ~override~
     "%MOD_FOLDER%/cre" ~override~
     "%MOD_FOLDER%/bam" ~override~
     "%MOD_FOLDER%/eff" ~override~
     "%MOD_FOLDER%/itm" ~override~
     "%MOD_FOLDER%/wav" ~override~
     "%MOD_FOLDER%/vvc" ~override~
     "%MOD_FOLDER%/bcs" ~override~
     "%MOD_FOLDER%/2da/LUZSSD.2DA" ~override~
     "%MOD_FOLDER%/2da/ZS#SDEVO.2DA" ~override~

// Kit insertion (BG2EE)
LAF ADD_KIT_EX
    INT_VAR
        help = RESOLVE_STR_REF(@0)
        briefdesc = RESOLVE_STR_REF(@0)
    STR_VAR
        kit_name = ~ZS_SHADOWDANCER~
        source_kit = ~SHADOWDANCER~
        backstab = ~2 2 2 2 3 3 3 3 4~
        thiefskl = ~40 15~
        clswpbon = ~0 0 3~
        clab_path = ~ZS_ShadowdancerOverhaul/2da/ZSSDCLAB.2da~
        luabbr = ~ZSSD~
    RET kit_id
END

COPY_EXISTING ~LUZSSD.2DA~ ~override~
    // self-concealment
    LPF patch_add_hla STR_VAR kit_name=~ZS_SHADOWDANCER~ ability= ~AP_ZS#SDH01~ num_allowed=~3~ prerequisite=~GA_SPCL938~ END
    // shadow master
    LPF patch_add_hla STR_VAR kit_name=~ZS_SHADOWDANCER~ ability=~AP_ZS#SDH02~ num_allowed=~1~ END
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~RR#THL01.SPL~ BEGIN
    COPY_EXISTING ~LUZSSD.2DA~ ~override~
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL919~ ability=~GA_RR#THL01~ prerequisite=~GA_SPCL916~ END
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL913~ ability=~GA_RR#THL02~ END
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL914~ ability=~GA_RR#THL03~ prerequisite=~GA_RR#THL02~ END
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL918~ ability=~GA_RR#ALCHT~ END
    BUT_ONLY
END ELSE BEGIN
    PRINT "Rogue Rebalancing HLA component not installed, skipping..."
END

INCLUDE "ZS_ShadowdancerOverhaul/libs/zs_process_spl.tph"
INCLUDE ~ZS_ShadowdancerOverhaul/libs/zs_remove_sd.tph~

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1003 DESIGNATED 300
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
//////////////////////////////////////////////////////////////////////////////////////////////////////

LAF generate_rogue_thac0 STR_VAR class1 = ~thief~ class2 = ~mage_thief~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1004 DESIGNATED 400
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
//////////////////////////////////////////////////////////////////////////////////////////////////////

LAF generate_rogue_thac0 STR_VAR class1 = ~bard~ END

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1006 DESIGNATED 410 // item weaks
REQUIRE_PREDICATE GAME_IS "bgee bg2ee eet" @10001
//////////////////////////////////////////////////////////////////////////////////////////////////////

INCLUDE ~ZS_ShadowdancerOverhaul/libs/component_scripts/zs_itemtweaks.tph~

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1005 DESIGNATED 5000
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
REQUIRE_COMPONENT "ZS_ShadowdancerOverhaul.tp2" "100" "This component requires the Shadowdancer Overhaul component"
//////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF FILE_EXISTS_IN_GAME  ~SPCL940.SPL~ BEGIN
    COPY_EXISTING ~SPCL940.SPL~ ~override~ // Spirit Form, adds the extra effects
        SAY UNIDENTIFIED_DESC @6000
        LPF a7_auto_apply_spl_effect // add all other resistances, immunity to detection by divination and nondetection
            INT_VAR
                def_target = 1  // Self
                def_duration = 30
                def_parameter1 = 50
                def_parameter2 = 0
            STR_VAR
                function_name = ~ADD_SPELL_EFFECT~
                effect_codes  = ~op=291,p2=1,p1=0;op=27;op=173;op=28;op=29;op=30;op=31;op=69,p1=0;op=204,p1=0,p2=3;op=0,p1=4;op=98,p1=1~
        END
        LPF ALTER_SPELL_HEADER INT_VAR header_type = 1 speed = 0 END
    BUT_ONLY
END ELSE BEGIN
    WARN "Spirit form not found in the game"
END

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1010 DESIGNATED 5001
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
//////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF FILE_EXISTS_IN_GAME ~RR#THL01.SPL~ BEGIN
    COPY_EXISTING ~LUTH4.2DA~ ~override~
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL919~ ability=~GA_RR#THL01~ prerequisite=~GA_SPCL916~ END
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL913~ ability=~GA_RR#THL02~ END
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL914~ ability=~GA_RR#THL03~ prerequisite=~GA_RR#THL02~ END
        LPF patch_replace_hla STR_VAR num_allowed=~99~ remove_ability=~GA_SPCL918~ ability=~GA_RR#ALCHT~ END
    BUT_ONLY
END ELSE BEGIN
    WARN "Rogue Rebalancing HLA component is not installed"
END

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1002 DESIGNATED 5002
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
//////////////////////////////////////////////////////////////////////////////////////////////////////

ACTION_IF FILE_EXISTS_IN_GAME ~SPCL936.SPL~ AND !(MOD_IS_INSTALLED ~ZS_ShadowdancerOverhaul.tp2~ ~100~) BEGIN
    COPY_EXISTING ~SPCL936.spl~ ~override~ // Shadow Twin, fixed wrong duration
        LPF ALTER_EFFECT INT_VAR match_duration = 60 duration = 120 END
    BUT_ONLY
END ELSE BEGIN
    PRINT "Shadow twin doesn't exist in game or fix was installed already through main component"
END

INCLUDE ~ZS_ShadowdancerOverhaul/libs/zs_fix_sd_prof_penalty.tph~

//////////////////////////////////////////////////////////////////////////////////////////////////////
BEGIN @1011 DESIGNATED 5003
REQUIRE_PREDICATE GAME_IS "iwdee bgee bg2ee eet" @10000
FORBID_COMPONENT ~ZS_ShadowdancerOverhaul.tp2~ ~100~ @10002
//////////////////////////////////////////////////////////////////////////////////////////////////////

COPY_EXISTING ~SIMULACR.spl~ ~override~ // fixes issue in 2.6.6 of simulacra's disabled Thief buttons
    LPF DELETE_EFFECT INT_VAR match_opcode=144 END
BUT_ONLY

//////////////////////////////////////////////////////////////////////////////////////////////////////
